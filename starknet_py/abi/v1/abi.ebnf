IDENTIFIER: /[a-zA-Z_][a-zA-Z_0-9]*/

start: type
     | struct
     | tuple

type: "core" "::" core_value
    | type_unit

core_value: "felt252"                                                   -> type_felt
          | "integer" "::" "u" INT                                      -> type_uint
          | "option" "::" "Option" "::" "<" (type | struct) ">"         -> type_option
          | "array" "::" "Array" "::" "<" (type | struct) ">"           -> type_array
          | "starknet" "::" "contract_address" "::" "ContractAddress"   -> type_address

type_unit: "()"

struct: (IDENTIFIER | "::")+ ("<" (type | ",")+ ">")?

tuple: "(" (start | ",")+ ")"


%import common.INT
%import common.WS
%ignore WS
